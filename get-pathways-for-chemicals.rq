#+ summary: Get molecular pathways that contain chemicals from AOP-Wiki.
#+ endpoint_in_url: False
#+ tags:
#+   - Pathways
#+ defaults:
#+   - KEfilter: rotenone

prefix cheminf: <http://semanticscience.org/resource/CHEMINF_> 
prefix aopo: <http://aopkb.org/aop_ontology#>
PREFIX wp: <http://vocabularies.wikipathways.org/wp#>

SELECT DISTINCT ?ChemicalName ?mappedid ?LinkedStressor (group_concat(?LinkedAOP;separator=',') as ?AOPs) (STR(?PathwayID) AS ?Pathway_ID) 
(STR(?PathwayTitle) AS ?PathwayName) ?PathwayURI 

WHERE{
   ?cheLook a cheminf:000000; dc:title ?ChemicalName ; dcterms:isPartOf ?LinkedStressor ;
      skos:exactMatch ?mappedid .
   ?LinkedStressor dcterms:isPartOf ?LinkedAOPURI .
   ?LinkedAOPURI a aopo:AdverseOutcomePathway ; rdfs:label ?LinkedAOP.
   ?mappedid a cheminf:000407; cheminf:000407 ?ChEBI.
   SERVICE <https://sparql.wikipathways.org/sparql>{ 
     ?metabolite wp:bdbChEBI ?mappedid ; dcterms:isPartOf ?PathwayURI.
     ?PathwayURI a wp:Pathway ; dcterms:identifier ?PathwayID ;  dc:title ?PathwayTitle.}
FILTER regex (?ChemicalName, ?_Chemfilter_en, "i")}
ORDER BY DESC (?ChemicalName)
